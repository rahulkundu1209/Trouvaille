response = Map();
response.put("action","context");
response.put("context_id",context_id);
if(context_id.startsWith("get_details"))
{
	pkgId = context_id.removeFirstOccurence("get_details_");
	get_details = answers.get("get_details").get("text");
	if(get_details.equalsIgnoreCase("No Thanks"))
	{
		response.put("action","end");
		response.put("replies",{"Have a great day"});
	}
	else
	{
		if(!answers.containsKey("email"))
		{
			question = {"name":"email","replies":{{"text":"Can I have your email address please?","field_name":"siq_email","validate":{"format":"email","error":{"Enter a valid email"}}}}};
			response.put("questions",{question});
		}
		else
		{
			email = answers.get("email").get("text");
			if(!answers.containsKey("name"))
			{
				question = {"name":"name","replies":{{"text":"Can I have your name please?","field_name":"siq_name"}}};
				response.put("questions",{question});
			}
			else
			{
				name = answers.get("name").get("text");
				if(!answers.containsKey("phone"))
				{
					question = {"name":"phone","replies":{{"text":"Can I have your phone number please?","validate":{"format":"phoneno","error":{"Enter a valid phone number"}},"field_name":"siq_phone"}}};
					response.put("questions",{question});
				}
				else
				{
					phone = answers.get("phone").get("text");
					// Move to the link
					lead_data = Map();
					lead_data.put("name","Travel Inquiry - " + name);
					lead_data.put("contact_name",name);
					lead_data.put("email_from",email);
					lead_data.put("phone",phone);
					lead_data.put("description","Package selected: " + pkgId);
					body = Map();
					body.put("vals_list",{lead_data});
					json_body = body.toString();
					// 					info json_body;
					odoo_response = invokeurl
					[
						url :"https://maa-jashoda-enterprise.odoo.com/json/2/crm.lead/create"
						type :POST
						body:json_body
						connection:"trouvallietocrm"
					];
					// 					info odoo_response;
					pkg_details_url = "https://script.google.com/macros/s/AKfycbxq7m_VIqYTnp4jwe4S-HonpXWRKb26qt5-iYrQYX5Hj1dny1M2FVgY4xO5Cdki0fTT/exec?action=getPackageById&id=" + pkgId;
					pkg_details_response = invokeurl
					[
						url :pkg_details_url
						type :GET
					];
					pkg_detail = pkg_details_response.get("package");
					// 					info pkg_detail;
					if(pkg_detail)
					{
						pkgTitle = pkg_detail.get("name");
						pkgPrice = pkg_detail.get("price");
						itinerary_list = pkg_detail.get("itinerary").toList(" | ");
						itinerary = "";
						for each  i in itinerary_list
						{
							itinerary = itinerary + i + "\n";
						}
						pkg_detail_reply = pkgTitle + "\nDays: " + pkg_detail.get("days") + "\nLocation: " + pkg_detail.get("location").toList(",").get(0) + "\nPrice: " + pkgPrice + "/-" + "\nItinerary: \n" + itinerary;
						reply_details = Map();
						reply_details.put("text",pkg_detail_reply);
						reply_details.put("image",pkg_detail.get("image_url"));
						reply_details.put("image_position","fill");
						book_date_input = {"type":"calendar","time":"false","tz":"true","from":"+1","to":"+60","label":"Book Now","select_label":"Select Date"};
						question = {"name":"book_date","replies":{reply_details,"Book Package: " + pkg_detail.get("name")},"input":book_date_input,"suggestions":{"Explore More"}};
						response.put("action","context");
						response.put("context_id","book_package");
						// 						response.put("replies",{reply_details});
						response.put("questions",{question});
						info response;
						session_map = Map();
						session_map.put("name",name);
						session_map.put("email",email);
						session_map.put("phone",phone);
						booking = {"package_id":pkgId,"package_title":pkgTitle,"package_price":pkgPrice};
						session_map.put("custom_info",booking);
						visitor_data_set_response = zoho.salesiq.visitorsession.set("maajashodaenterprise",session_map,"salesiqconnection");
						// 						info visitor_data_set_response;
					}
					else
					{
						pkg_detail_reply = "Sorry! Some Error Occured!";
						response.put("replies",{pkg_detail_reply});
					}
				}
			}
		}
	}
}
else if(context_id.equals("take_preference"))
{
	// 	info "Taking preference!";
	if(!answers.containsKey("location_preference"))
	{
		question = {"name":"location_preference","replies":{"Please enter your preferred location."}};
		response.put("questions",{question});
	}
	else
	{
		location_preference = answers.get("location_preference").get("text");
		if(!answers.containsKey("days_preference"))
		{
			days_input = {"type":"range-slider","values":{4,8,12,16,20,30,50}};
			question = {"name":"days_preference","replies":{"How many days you are planning to travel?"},"input":days_input};
			response.put("questions",{question});
		}
		else
		{
			days_preference = answers.get("days_preference").get("text");
			if(!answers.containsKey("budget_preference"))
			{
				budget_input = {"type":"range-slider","values":{1000,4000,8000,12000,16000,20000,30000,50000}};
				question = {"name":"budget_preference","replies":{"What is your budget range?"},"input":budget_input};
				response.put("questions",{question});
			}
			else
			{
				// 				info answers;
				budget_preference = answers.get("budget_preference").get("text");
				preferences = {"location":location_preference,"days":days_preference.toList(" - "),"budget":budget_preference.toList(" - ")};
				preferences.put("action","filterPackages");
				payload = preferences.toString();
				res = invokeurl
				[
					url :"https://script.google.com/macros/s/AKfycbzAop25rUw_MGAHS5uJnoiB_uzFd3vdAqbwrBylaBM1ASz3-FFOspikV1P2FcBrS2Bi/exec"
					type :POST
					body:payload
					detailed:true
				];
				resHeader = res.get("responseHeader");
				redirectUrl = resHeader.get("location");
				filter_res = getUrl(redirectUrl,true);
				// 				info filter_res;
				packages = filter_res.get("packages");
				// Create Carousel Cards
				if(packages.size() != 0)
				{
					elements = List();
					for each  pkg in packages
					{
						// 						info pkg;
						element = Map();
						element.put("title",pkg.get("name"));
						element.put("subtitle",pkg.get("description"));
						element.put("id",pkg.get("id"));
						element.put("image",pkg.get("image_url"));
						// 	info element;
						// optional actions
						actions = List();
						act = Map();
						act.put("label","Get Details");
						act.put("name","get_details_" + pkg.get("id"));
						act.put("type",null);
						// or "url" depending on behavior
						act.put("link","_blank");
						actions.add(act);
						element.put("actions",actions);
						elements.add(element);
					}
					prodWidget = Map();
					prodWidget.put("type","multiple-product");
					prodWidget.put("text","Here are our featured packages");
					prodWidget.put("elements",elements);
					replies = List();
					replies.add(prodWidget);
					response.put("action","reply");
					response.put("replies",replies);
					info response;
				}
				else
				{
					replies = List();
					replies.add("Sorry! No such package available!");
					response.put("action","reply");
					response.put("replies",replies);
				}
			}
		}
	}
}
else if(context_id.equals("book_package"))
{
	// 	info answers;
	name = zoho.salesiq.visitorsession.get("maajashodaenterprise","name","salesiqconnection").get("data").get("name");
	email = zoho.salesiq.visitorsession.get("maajashodaenterprise","email","salesiqconnection").get("data").get("email");
	phone = zoho.salesiq.visitorsession.get("maajashodaenterprise","phone","salesiqconnection").get("data").get("phone");
	book_pkg = zoho.salesiq.visitorsession.get("maajashodaenterprise","custom_info","salesiqconnection").get("data").get("custom_info");
	book_date = answers.get("book_date").get("text").toList(" ").get(0).toString("yyyy-MM-dd");
	info book_date;
	if(!name || !email || !phone || !book_pkg)
	{
		response.put("action","reply");
		response.put("replies",{"Invalid Booking Request!"});
	}
	else
	{
		//Display Booking Informations for confirmation
		if(!answers.containsKey("proceed_status"))
		{
			question = {"name":"proceed_status","replies":{"Can you please confirm the following?" + "\nName: " + name + "\nEmail: " + email + "\nPackage: " + book_pkg.get("package_title") + "\nPrice: " + book_pkg.get("package_price") + "/-"},"input":{"type":"select","options":{"Confirm","Edit","Cancel Booking"}}};
			response.put("questions",{question});
		}
		else
		{
			proceed_status = answers.get("proceed_status").get("text");
			if(proceed_status.equalsIgnoreCase("Confirm"))
			{
				response.put("action","reply");
				response.put("replies",{"Invoice sent to email!"});
				//Send the Invoice
				//First check if the contact is present, if not create one.
				invoice_response = invokeurl
				[
					url :"https://www.zohoapis.com/invoice/v3/contacts"
					type :GET
					connection:"packagebookinginvoice"
				];
				// 				info invoice_response;
				contacts = invoice_response.get("contacts");
				contact_id = "";
				for each  contact in contacts
				{
					if(contact.get("email") == email)
					{
						contact_id = contact.get("contact_id");
					}
				}
				if(contact_id == "")
				{
					contact_body = {"contact_name":name,"contact_persons":{{"email":email,"phone":phone,"is_primary_contact":true}}};
					body = contact_body.toString();
					invoice_response = invokeurl
					[
						url :"https://www.zohoapis.com/invoice/v3/contacts"
						type :POST
						body:body
						connection:"packagebookinginvoice"
					];
					// 					info invoice_response;
					contact_id = invoice_response.get("contact").get("contact_id");
				}
				// Write invoice POST request body
				invoice_body = {"customer_id":contact_id,"date":today.toString("yyyy-MM-dd"),"line_items":{{"item_id":book_pkg.get("package_id"),"name":book_pkg.get("package_title") + " Date: " + book_date,"rate":book_pkg.get("package_price"),"quantity":1}}};
				body = invoice_body.toString();
				invoice_response = invokeurl
				[
					url :"https://www.zohoapis.com/invoice/v3/invoices"
					type :POST
					body:body
					connection:"packagebookinginvoice"
				];
				// 				info invoice_response;
				invoice_id = invoice_response.get("invoice").get("invoice_id");
				// Send the invoice to the user
				mail_body = {"send_from_org_email_id":true,"to_mail_ids":{email},"cc_mail_ids":{"rk1209.work@gmail.com"},"subject":"Invoice from Maa Jashoda Enterprise for Booking: " + book_pkg.get("package_title"),"body":"Dear Customer, Thank You for Booking!"};
				body = mail_body.toString();
				if(invoice_id)
				{
					invoice_response = invokeurl
					[
						url :"https://www.zohoapis.com/invoice/v3/invoices/" + invoice_id + "/email"
						type :POST
						body:body
						connection:"packagebookinginvoice"
					];
					info invoice_response;
					//Store the Booking Informations in Google Sheet
					booking_body = {"action":"saveBooking","id":invoice_id,"package_id":book_pkg.get("package_id"),"email":email,"start_date":book_date};
					res = invokeurl
					[
						url :"https://script.google.com/macros/s/AKfycbxurijHPMuxGP7L2hV2-3TsvIVeg9hEFtnmzqXRLqa4ISCO930cyR9V3aYViiLxu1rU/exec"
						type :POST
						body:booking_body.toString()
						detailed:true
					];
					resHeader = res.get("responseHeader");
					redirectUrl = resHeader.get("location");
					sheet_response = getUrl(redirectUrl,true);
					info sheet_response;
				}
			}
			else if(proceed_status.equalsIgnoreCase("Cancel Booking"))
			{
				response.put("action","reply");
				response.put("replies",{"Stopped Booking"});
			}
			else if(proceed_status.equalsIgnoreCase("Edit"))
			{
				response.put("action","reply");
				response.put("replies",{"Editing Information"});
			}
		}
	}
}
else if(context_id.equals("prev_booked_packages"))
{
	email = answers.get("email").get("text");
	session_map = Map();
	session_map.put("email",email);
	visitor_data_set_response = zoho.salesiq.visitorsession.set("maajashodaenterprise",session_map,"salesiqconnection");
	request_body = {"action":"getBookingsByEmail","email":email};
	// 	info request_body;
	body = request_body.toString();
	res = invokeurl
	[
		url :"https://script.google.com/macros/s/AKfycbx64PGfGZ6DYCABl92nB3kMgbz1jAGG4oJI7bScCbL_1B1iEYlXEIsIvGGWg76Oe7MW/exec"
		type :POST
		body:body
		detailed:true
	];
	// 	info res;
	resHeader = res.get("responseHeader");
	redirectUrl = resHeader.get("location");
	booking_res = getUrl(redirectUrl,true);
	// 	info booking_res;
	bookings = booking_res.get("bookings");
	// 	info bookings;
	packages = List();
	count = 0;
	for each  booking in bookings
	{
		count = count + 1;
		// 		feedback_input = {"type": "star-rating", "level": "5" };
		action1 = {"label":"Give Feedback","name":"feedback_" + booking.get("id"),"type":null,"link":"_blank"};
		action2 = {"label":"Book Again","name":"book_again_" + booking.get("package_id"),"type":null,"link":"_blank"};
		subtitle = "Location: " + booking.get("package").get("location").toList(",").get(0) + " | " + "Date: " + booking.get("start_date");
		elements = {{"id":booking.get("package_id"),"image":booking.get("package").get("image_url"),"title":booking.get("package").get("name"),"subtitle":subtitle,"actions":{action1,action2}}};
		package = Map();
		package.put("type","multiple-product");
		package.put("text","Booking: " + count);
		// 		package.put();
		// 		package.put();
		package.put("elements",elements);
		// 		package.put("actions",{action1,action2});
		packages.add(package);
	}
	response.put("action","reply");
	if(packages.size() != 0)
	{
		response.put("replies",packages);
	}
	else
	{
		response.put("replies",{"You don't have any booked package!"});
		response.put("suggestions",{"Show By My Preference"});
	}
	info response;
}
else if(context_id.startsWith("book_again_"))
{
	pkgId = context_id.remove("book_again_");
	name = zoho.salesiq.visitorsession.get("maajashodaenterprise","name","salesiqconnection").get("data").get("name");
	email = zoho.salesiq.visitorsession.get("maajashodaenterprise","email","salesiqconnection").get("data").get("email");
	phone = zoho.salesiq.visitorsession.get("maajashodaenterprise","phone","salesiqconnection").get("data").get("phone");
	info email;
	info name;
	info phone;
	if(email == null && !answers.containsKey("email"))
	{
		question = {"name":"email","replies":{{"text":"Can I have your email address please?","field_name":"siq_email","validate":{"format":"email","error":{"Enter a valid email"}}}}};
		response.put("questions",{question});
	}
	else
	{
		email = answers.get("email").get("text");
		session_map = Map();
		session_map.put("email",email);
		visitor_data_set_response = zoho.salesiq.visitorsession.set("maajashodaenterprise",session_map,"salesiqconnection");
		if(name == null && !answers.containsKey("name"))
		{
			question = {"name":"name","replies":{{"text":"Can I have your name please?","field_name":"siq_name"}}};
			response.put("questions",{question});
		}
		else
		{
			name = answers.get("name").get("text");
			session_map = Map();
			session_map.put("name",name);
			visitor_data_set_response = zoho.salesiq.visitorsession.set("maajashodaenterprise",session_map,"salesiqconnection");
			if(phone == null && !answers.containsKey("phone"))
			{
				question = {"name":"phone","replies":{{"text":"Can I have your phone number please?","validate":{"format":"phoneno","error":{"Enter a valid phone number"}},"field_name":"siq_phone"}}};
				response.put("questions",{question});
			}
			else
			{
				phone = answers.get("phone").get("text");
				session_map = Map();
				session_map.put("phone",phone);
				pkg_details_url = "https://script.google.com/macros/s/AKfycbxq7m_VIqYTnp4jwe4S-HonpXWRKb26qt5-iYrQYX5Hj1dny1M2FVgY4xO5Cdki0fTT/exec?action=getPackageById&id=" + pkgId;
				pkg_details_response = invokeurl
				[
					url :pkg_details_url
					type :GET
				];
				pkg_detail = pkg_details_response.get("package");
				// 					info pkg_detail;
				booking = Map();
				if(pkg_detail)
				{
					booking.put("package_id",pkgId);
					pkgTitle = pkg_detail.get("name");
					booking.put("package_title",pkgTitle);
					pkgPrice = pkg_detail.get("price");
					booking.put("package_price",pkgPrice);
					session_map.put("custom_info",booking);
					visitor_data_set_response = zoho.salesiq.visitorsession.set("maajashodaenterprise",session_map,"salesiqconnection");
					book_date_input = {"type":"calendar","time":"false","tz":"true","from":"+1","to":"+60","label":"Book Now","select_label":"Select Date"};
					question = {"name":"book_date","replies":{"Book Package: " + pkgTitle},"input":book_date_input,"suggestions":{"Explore More"}};
					response.put("action","context");
					response.put("context_id","book_package");
					// 						response.put("replies",{reply_details});
					response.put("questions",{question});
				}
				else
				{
					response.put("reply","end");
					response.put("replies",{"Wrong Information"});
				}
			}
		}
	}
}
else if(context_id.startsWith("give_feedback_"))
{
	info answers;
	email = zoho.salesiq.visitorsession.get("maajashodaenterprise","email","salesiqconnection").get("data").get("email");
	pkgId = context_id.remove("give_feedback_");
	rating = answers.get("feedback_rating").get("text");
	if(!answers.containsKey("feedback_comment"))
	{
		question = {"name":"feedback_comment","replies":{{"text":"Write a comment please.","field_name":"siq_comment"}}};
		response.put("questions",{question});
	}
	else
	{
		feedback_comment = answers.get("feedback_comment").get("text");
		feedback_body = {"action":"saveFeedback","id":pkgId,"feedback_rating":rating,"feedback_comment":feedback_comment};
		body = feedback_body.toString();
		res = invokeurl
		[
			url :"https://script.google.com/macros/s/AKfycbx64PGfGZ6DYCABl92nB3kMgbz1jAGG4oJI7bScCbL_1B1iEYlXEIsIvGGWg76Oe7MW/exec"
			type :POST
			body:body
		];
		info res;
		response.put("replies",{"Thanks for the feedback!"});
	}
}
else
{
	info "blank context";
}
return response;